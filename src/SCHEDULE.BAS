'----------------------------------------
'          SCHEDULE Subroutine
'----------------------------------------
'This subroutine gives the user several
'options for interfacing with the Scheduler
'and also user boxscores generated by
'auto-played games
Sub SCHEDULE ()

    Dim scheduleFile$
    ReDim menuChoices$(1 To 10)

    Shared NS%

    Shared scheduleAP%(), scheduleNG%()

    _Title "Courtside College Basketball - Scheduler"

    Do

        Color 7, 0
        Cls

        Call TITLE_SCHEDULER

        Locate LAST_ROW - 4, 1
        Color 2
        Print "GENERATING A SEASON SCHEDULE MUST BE USED IN CONJUNCTION WITH"
        Print "STANDINGS AND CONFERENCE LEADERS OPTIONS. YOU MUST ALREADY HAVE CREATED"
        Print "AN APPROPRIATE CONFERENCE FOR THIS OPTION TO FUNCTION."

        menuChoices$(1) = "ENTER A NEW SCHEDULE              "
        menuChoices$(2) = "PLAY / EDIT AN EXISTING SCHEDULE  "
        menuChoices$(3) = "EXTRACT TEAM SCHEDULE             "
        menuChoices$(4) = "EXTRACT CONFERENCE SCHEDULE       "
        menuChoices$(5) = "CHANGE TEAM CONTROL FOR A SCHEDULE"
        menuChoices$(6) = "MAKE GLOBAL CHANGES TO A SCHEDULE "
        menuChoices$(7) = "GENERATE A CONFERENCE SCHEDULE    "
        menuChoices$(8) = "BOXSCORE MAINTENANCE / PARAMETERS "
        menuChoices$(9) = "CONVERT SCHEDULE FILE FROM 4.0    "
        menuChoices$(10) = "RETURN TO COURTSIDE CB MAIN MENU  "
        SELECTION% = 1
        Count% = 10
        Start% = 1
        ExitCode% = 10
        FileFGColor = 15: FileBGColor = 1
        HiLiteFGColor% = 0: HiLiteBGColor% = 7
        Spacing% = 1
        Row% = 8
        menuWidth% = Len(menuChoices$(1))
        Column% = (80 - menuWidth% - 2) / 2

        Call box0(Row% - 1, Column% - 1, Row% + Count%, Column% + menuWidth%, 2, FileFGColor, FileBGColor)

        _MouseShow "DEFAULT"
        MMenuVert menuChoices$(), SELECTION%, Start%, ExitCode%, FileFGColor, FileBGColor, HiLiteFGColor%, HiLiteBGColor%, Count%, Row%, Column%
        _MouseHide

        Color , 0

        If ExitCode% <> 27 Then

            'Assign full array sizes for schedule games
            ReDim homeScores(MAX_SCHEDULE_GAMES)
            ReDim visitorScores(MAX_SCHEDULE_GAMES)
            ReDim scheduleH$(MAX_SCHEDULE_GAMES)
            ReDim scheduleV$(MAX_SCHEDULE_GAMES)

            scheduleNG%(0, 2) = 1 'Home Court
            scheduleNG%(0, 3) = 1 '3 Pt Opt
            scheduleNG%(0, 5) = 5 'Fouls to DQ
            scheduleNG%(0, 6) = 0 'FT 10th Foul
            scheduleNG%(0, 7) = 3 'Shot Clock
            scheduleNG%(0, 8) = 3 'Team Control
            scheduleNG%(0, 9) = 1 'Coach Opt
            scheduleNG%(0, 10) = 1 'Foul on 3 PT
            scheduleNG%(0, 14) = 0 'FT on 6 Floor

            Select Case SELECTION%

                Case 1:
                    'number of games in schedule
                    scheduleAP%(0) = 0
                    NS% = 0

                    scheduleFile$ = _InputBox$("Schedule File Name", "Enter name for schedule file")

                    If scheduleFile$ <> "" Then
                        Call NewSchedule(scheduleFile$)
                    End If

                Case 2 To 6:

                    Do

                        'Have the user pick a schedule
                        scheduleFile$ = _OpenFileDialog$("Choose Schedule file", diskPaths$(3), "*.SCD", "Schedule files [.SCD]")
                        'ChooseFile$(diskPaths$(3) + "*.SCD", "SCHEDULE", FileExit%)

                        If scheduleFile$ <> "" Then

                            Call LoadScheduleInfo(scheduleFile$)

                            Cls

                            'Extract / Change / Global
                            If SELECTION% >= 3 And SELECTION% <= 6 Then
                                Call ExtractSchedule(SELECTION%, scheduleFile$)
                                If SELECTION% >= 5 Then Call SaveScheduleFile(scheduleFile$)
                            Else
                                'Retrieve / Edit / Play Schedule
                                Color 15, 0
                                Call NewSchedule(scheduleFile$)
                            End If

                        End If

                    Loop Until FileExit% = 27 Or FileExit% = 0

                Case 7:
                    Call CreateConferenceSchedule

                Case 8:
                    Call BoxscoreMaintenance

                Case 9
                    scheduleFile$ = _OpenFileDialog$("Select schedule file", diskPaths$(3), "*.SCD", "Schedule files [.SCD]")
                    Call ConvertSched4to5(scheduleFile$, 0)
                    'Call ConvertAllSched

            End Select

        End If

    Loop Until SELECTION% = 10 Or ExitCode% = 27

    'Free up memory for rest of program
    ReDim homeScores(1)
    ReDim visitorScores(1)

    'Restore array sizes for TOURNAMENT routines
    ReDim homeScores(MAX_TOURNAMENT_GAMES)
    ReDim visitorScores(MAX_TOURNAMENT_GAMES)

End Sub

'----------------------------------------
'       NewSchedule SubRoutine
'----------------------------------------
'This subroutine facilitates the user
'creation of a schedule. It provides all
'options required and displays the schedule
'while editing
Sub NewSchedule (scheduleFile$)

    Shared BS%, NS%

    Shared homeScores(), visitorScores()
    Shared scheduleAP%(), scheduleNG%()
    Shared scheduleH$(), scheduleV$()
    Shared scheduleYN$()

    Shared modeAbbrev$(), sClockOpt$(), yesNo$()

    DT$ = "11-04-25"
    TM$ = "07:00P"

    'Show 25 schedule entries on the screen at once
    baseFile$ = GetFileBaseName$(scheduleFile$)

    Do

        Color 15, 0
        Cls

        Locate LAST_ROW - 2, 1
        Color , 1: Print Tab(80); " ";
        Locate LAST_ROW - 2, 1
        Color 13: Print "SC";
        Color 14: Print "-SHOT CLOCK  ";
        Color 13: Print "MA";
        Color 14: Print "-MODE OF PLAY  ";
        Color 11: Print " G";
        Color 14: Print "-GOTO GAME";
        Color 11: Print " B";
        Color 14: Print "-BOXSCORE OPTIONS"

        Locate LAST_ROW - 1, 1
        Color , 1: Print Tab(80); " ";
        Locate LAST_ROW - 1, 1
        Color 11: Print "C";
        Color 14: Print "-SAVE AND CONTINUE  ";
        Color 11: Print "A";
        Color 14: Print "-ADD A GAME  ";
        Color 11: Print "D";
        Color 14: Print "-DELETE A GAME  ";
        Color 11: Print "E";
        Color 14: Print "-EDIT A GAME  ";
        Color 11: Print "PGUP  PGDN";

        Locate LAST_ROW, 1
        Color , 1: Print Tab(80); " ";
        Locate LAST_ROW, 1
        Color 11: Print "S";
        Color 14: Print "-SORT GAMES  ";
        Color 11: Print "P";
        Color 14: Print "-PLAY GAMES   ";
        Color 11: Print "X";
        Color 14: Print "-SAVE AND EXIT  ";
        Color 11: Print "Q";
        Color 14: Print "-QUIT  ";
        Color 11: Print "R";
        Color 14: Print "-RESET SCORES";

        Locate 1, 1: Print Tab(80); " ";
        Locate 1, 1: Print baseFile$
        Locate 1, 33

        Color 14: Print "AUTO SCHEDULER"

        Color 15, 0

        Locate 2, 1
        Color 14
        Print "GAME  DATE      TIME    VISITING TEAM        HOME TEAM            MA  HC  3  SC"
        Color 15

        For X = 1 To SCHED_ENTRIES_PER_SCREEN

            Locate X + 2, 1

            If X + NS% * SCHED_ENTRIES_PER_SCREEN <= scheduleAP%(0) Then

                T1 = scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 0)
                YN$ = scheduleYN$(X + NS% * SCHED_ENTRIES_PER_SCREEN, 0)
                Call ReadTeam(YN$, team$, T1)
                Call ReadExtraData(YN$, T1, validData)

                If validData = 1 Then

                    scheduleV$(X + NS% * SCHED_ENTRIES_PER_SCREEN) = team$

                    T1 = scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 1)
                    YN$ = scheduleYN$(X + NS% * SCHED_ENTRIES_PER_SCREEN, 1)
                    Call ReadTeam(YN$, team$, T1)
                    Call ReadExtraData(YN$, T1, validData)

                    If validData = 1 Then

                        scheduleH$(X + NS% * SCHED_ENTRIES_PER_SCREEN) = team$

                        'Game number
                        Print Using "####  "; X + NS% * SCHED_ENTRIES_PER_SCREEN;

                        'Date
                        Print Using "##/##/##"; scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 17); scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 16); scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 18);

                        'Time
                        Locate , 17
                        I$ = "A"
                        HR% = scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 15) / 100
                        MIN% = scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 15) - HR% * 100

                        If HR% = 12 Then I$ = "P"
                        If HR% > 12 Then HR% = HR% - 12: I$ = "P"

                        TM$ = Right$(Str$(HR%), 2) + ":"

                        If MIN% < 10 Then TM$ = TM$ + "0" + Right$(Str$(MIN%), 1)
                        If MIN% >= 10 Then TM$ = TM$ + Right$(Str$(MIN%), 2)

                        'Time
                        TM$ = TM$ + I$
                        Print TM$;

                        'Visiting Team
                        Locate , 25: Print scheduleV$(X + NS% * SCHED_ENTRIES_PER_SCREEN); " ";
                        If scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 13) = 1 Then Print Using "###"; visitorScores(X + NS% * SCHED_ENTRIES_PER_SCREEN);

                        'Home Team
                        Locate , 46: Print scheduleH$(X + NS% * SCHED_ENTRIES_PER_SCREEN); " ";
                        If scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 13) = 1 Then Print Using "###"; homeScores(X + NS% * SCHED_ENTRIES_PER_SCREEN);

                        'Mode of Play
                        Locate , 67: Print modeAbbrev$(scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 8));

                        'Home Court
                        Locate , 72: If scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 2) = 1 Then Print "Y";: Else Print "N";

                        '3 Pt Option
                        Locate , 75: Print yesNo$(scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 3));

                        'Shot Clock
                        Locate , 78: Print Left$(sClockOpt$(scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 7)), 2)

                    End If

                End If

                If validData = 0 Then
                    'Invalid team file
                    Locate 9,
                    Call CSMsgBox("THERE WAS AN ISSUE READING TEAM DATA. CHECK THE TEAM FILE FOR ANY ERRORS... PRESS ANY KEY AND TRY ANOTHER SCHEDULE", 40, PASS)
                    K$ = GetKeyPress$
                    Call CSMsgBox("", 0, PASS)
                    Exit Sub
                End If

            End If

        Next

        I$ = GetKeyPress$

        Select Case UCase$(I$)

            Case "A":
                ' *** ADD A GAME TO THE SCHEDULE ***
                V$ = ""
                H$ = ""

                'number of games in schedule
                scheduleAP%(0) = scheduleAP%(0) + 1
                targetGame% = scheduleAP%(0)

                BS% = Int((targetGame% - 1) / SCHED_ENTRIES_PER_SCREEN)
                If NS% < BS% Then NS% = BS%

                For X = 0 To 18
                    If X <> 13 Then
                        scheduleNG%(targetGame%, X) = scheduleNG%(targetGame% - 1, X)
                    End If
                Next

                For X = 0 To 1:
                    scheduleYN$(targetGame%, X) = scheduleYN$(targetGame% - 1, X)
                Next

                Call NewGame(V$, H$, YN$, DT$, TM$, NTMS, targetGame%)

            Case "E":
                Call EditGame(DT$, TM$)

            Case "B":
                Do
                    Locate LAST_ROW - 3, 1: Print "ENTER GAME # TO GO TO        "
                    Locate LAST_ROW - 3, 1: Input "ENTER GAME # TO GO TO "; targetGame%
                Loop Until targetGame% >= 1 And targetGame% <= scheduleAP%(0)

                NS% = Int((targetGame% - 1) / SCHED_ENTRIES_PER_SCREEN)

                'boxName$ = diskPaths$(1) + "BOX" + Mid$(Str$(targetGame%), 2, Len(Str$(targetGame%)))
                boxName$ = diskPaths$(1) + baseFile$ + "_GAME" + Mid$(Str$(targetGame%), 2, Len(Str$(targetGame%))) + ".BS"

                Call EditBoxscore(boxName$)
                Color 15, 0

            Case "D":
                ' *** DELETE A GAME FROM THE SCHEDULE ***
                Do
                    Locate 24, 1: Input "ENTER GAME # TO DELETE "; targetGame%
                Loop Until targetGame% >= 1 And targetGame% <= scheduleAP%(0)

                For X = targetGame% To scheduleAP%(0) - 1
                    For X1 = 0 To 18
                        scheduleNG%(X, X1) = scheduleNG%(X + 1, X1)
                    Next

                    visitorScores(X) = visitorScores(X + 1)
                    homeScores(X) = homeScores(X + 1)

                    For X1 = 0 To 1
                        scheduleYN$(X, X1) = scheduleYN$(X + 1, X1)
                    Next
                Next

                scheduleAP%(0) = scheduleAP%(0) - 1
                BS% = Int((scheduleAP%(0) - 1) / SCHED_ENTRIES_PER_SCREEN)
                If NS% > BS% Then NS% = BS%

            Case "G":
                'GOTO A SPECIFIC PAGE
                Do
                    Locate LAST_ROW - 3, 1: Print "ENTER GAME # TO GO TO        "
                    Locate LAST_ROW - 3, 1: Input "ENTER GAME # TO GO TO "; targetGame%
                Loop Until targetGame% >= 1 And targetGame% <= scheduleAP%(0)

                NS% = Int((targetGame% - 1) / SCHED_ENTRIES_PER_SCREEN)

            Case "S":
                Call SortSchedule

            Case "C", "X":
                Call SaveScheduleFile(scheduleFile$)

            Case "Q", Chr$(27): '

            Case "P":
                Call SaveScheduleFile(scheduleFile$)

                ' *** PLAY GAMES ***
                Color , 0
                Cls

                Call MenuBox(6, 8, 61, 8, 15, 4)

                Do

                    Locate 8, 12: Print "TOTAL GAMES ="; scheduleAP%(0)
                    Locate 9, 12: Print "SELECT GAME # TO START AND END WITH": Print
                    Locate 10, 12: Input "ENTER START GAME #: "; SG%: Print
                    Locate 11, 12: Input "ENTER END GAME #:"; EG%: Print

                    Do
                        Locate 13, 12: Print "ANY CHANGE? (Y/N)"
                        I$ = GetKeyPress$
                    Loop Until UCase$(I$) = "N" Or UCase$(I$) = "Y"

                Loop Until UCase$(I$) = "N"

                If SG% <= 0 Then SG% = 1
                If EG% > scheduleAP%(0) Then EG% = scheduleAP%(0)

                Open "GAME82" For Output As #1
                Print #1, scheduleFile$
                Print #1, SG% 'Start Game
                Print #1, EG% 'End Game
                Print #1, 0
                Close #1

                For I = 1 To scheduleAP%(0)
                    HR% = Int(scheduleNG%(I, 15) / 100)
                    MIN% = scheduleNG%(I, 15) - HR% * 100
                    TM% = HR% * 60 + MIN%
                    scheduleNG%(I, 12) = Int(TM% / 30) - 18
                Next

                Open "TICKER" For Output As #2

                For I = 1 To 32:
                    For I1 = 0 To 1:
                        For I2 = 1 To 14
                            Print #2, -1
                        Next
                    Next
                Next

                For I = 1 To 32:
                    For I1 = 0 To 1:
                        Print #2, "XXX"
                    Next
                Next

                For I = SG% To SG% + 31
                    Print #2, scheduleNG%(I, 12)
                Next

                Close #2

                'Free up used memory so games can load faster
                ReDim scheduleNG%(1, 18)
                ReDim scheduleYN$(1, 1)
                ReDim homeScores(1)
                ReDim visitorScores(1)
                ReDim scheduleH$(1)
                ReDim scheduleV$(1)

                Do
                    Call COLHOOP
                Loop Until schedGame(0) + schedGame(2) > schedGame(1)

                'Restore memory so we can populate again
                ReDim scheduleNG%(MAX_SCHEDULE_GAMES, 18)
                ReDim scheduleYN$(MAX_SCHEDULE_GAMES, 1)
                ReDim homeScores(MAX_SCHEDULE_GAMES)
                ReDim visitorScores(MAX_SCHEDULE_GAMES)
                ReDim scheduleH$(MAX_SCHEDULE_GAMES)
                ReDim scheduleV$(MAX_SCHEDULE_GAMES)

                Call LoadScheduleInfo(scheduleFile$)

            Case "R":
                ' *** RESET GAMES ***
                For I = 1 To scheduleAP%(0)
                    scheduleNG%(I, 13) = 0
                Next

            Case "Z"
                'Export to text file
                Open "schedule" For Append As #15

                For X = 1 To scheduleAP%(0)

                    T1 = scheduleNG%(X, 0)
                    YN$ = scheduleYN$(X, 0)
                    Call ReadTeam(YN$, team$, T1)
                    scheduleV$(X) = team$

                    T1 = scheduleNG%(X, 1)
                    YN$ = scheduleYN$(X, 1)
                    Call ReadTeam(YN$, team$, T1)
                    scheduleH$(X) = team$

                    'Game number
                    Print #15, Using "####"; X;
                    Print #15, ",";

                    'Date
                    Print #15, Using "##/##/##"; scheduleNG%(X, 17); scheduleNG%(X, 16); scheduleNG%(X, 18);
                    Print #15, ",";

                    'Time
                    I$ = "A"
                    HR% = scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 15) / 100
                    MIN% = scheduleNG%(X + NS% * SCHED_ENTRIES_PER_SCREEN, 15) - HR% * 100

                    If HR% = 12 Then I$ = "P"
                    If HR% > 12 Then HR% = HR% - 12: I$ = "P"

                    TM$ = Right$(Str$(HR%), 2) + ":"

                    If MIN% < 10 Then TM$ = TM$ + "0" + Right$(Str$(MIN%), 1)
                    If MIN% >= 10 Then TM$ = TM$ + Right$(Str$(MIN%), 2)

                    'Time
                    TM$ = TM$ + I$
                    Print #15, TM$; ",";

                    'Visiting Team
                    Print #15, scheduleV$(X); ",";
                    If scheduleNG%(X, 13) = 1 Then
                        Print #15, Using "###"; visitorScores(X);
                    End If
                    Print #15, ",";

                    'Home Team
                    Print #15, scheduleH$(X); ",";
                    If scheduleNG%(X, 13) = 1 Then
                        Print #15, Using "###"; homeScores(X);
                    End If

                    Print #15,

                Next X

                Close #15

            Case Chr$(0) + Chr$(81), Chr$(0) + Chr$(77)
                ' *** PAGE DOWN | RIGHT ARROW ***
                If NS% >= BS% Then
                    NS% = BS%
                Else
                    NS% = NS% + 1
                End If

            Case Chr$(0) + Chr$(73), Chr$(0) + Chr$(75)
                ' *** PAGE UP | LEFT ARROW ***
                If NS% <> 0 Then
                    NS% = NS% - 1
                End If

        End Select

    Loop Until UCase$(I$) = "Q" Or UCase$(I$) = "X"

End Sub


'----------------------------------------
'          EditGame SubRoutine
'----------------------------------------
'This subroutine gives the user an inter-
'face for choosing games to edit, then
'calls the NewGame routine to essentially
'display the existing parameters and allow
'them to be modified
Sub EditGame (DT$, TM$)

    Shared scheduleAP%()
    Shared scheduleH$(), scheduleV$()
    Shared scheduleYN$()
    Shared scheduleNG%()

    Shared NS%

    ' *** EDIT A GAME ***
    Do
        Locate LAST_ROW - 3, 1: Print "ENTER GAME # TO EDIT        "
        Locate LAST_ROW - 3, 1: Input "ENTER GAME # TO EDIT "; targetGame%
    Loop Until targetGame% >= 1 And targetGame% <= scheduleAP%(0)

    T1 = scheduleNG%(targetGame%, 0)
    YN$ = scheduleYN$(targetGame%, 0)
    Call ReadTeam(YN$, team$, T1)
    Call ReadExtraData(YN$, T1, validData)

    If validData = 1 Then

        V$ = team$

        T1 = scheduleNG%(targetGame%, 1)
        YN$ = scheduleYN$(targetGame%, 1)
        Call ReadTeam(YN$, team$, T1)
        Call ReadExtraData(YN$, T1, validData)

        If validData = 1 Then

            H$ = team$

        End If

    End If

    HR% = Int(scheduleNG%(targetGame%, 15) / 100)
    MIN% = scheduleNG%(targetGame%, 15) - HR% * 100

    If HR% >= 12 Then
        I$ = "P"
        If HR% > 12 Then HR% = HR% - 12
    Else
        I$ = "A"
    End If

    TM$ = Right$(Str$(HR%), 2) + ":"

    If MIN% < 10 Then
        TM$ = TM$ + "0" + Right$(Str$(MIN%), 1)
    Else
        TM$ = TM$ + Right$(Str$(MIN%), 2)
    End If

    TM$ = TM$ + I$

    DT$ = Right$(Str$(scheduleNG%(targetGame%, 17)), 2) + "/"
    DYMO% = Val(Right$(Str$(scheduleNG%(targetGame%, 16)), 2))

    If DYMO% < 10 Then
        DYMO$ = "0" + Right$(Str$(scheduleNG%(targetGame%, 16)), 1)
    Else
        DYMO$ = Right$(Str$(scheduleNG%(targetGame%, 16)), 2)
    End If

    DT$ = DT$ + DYMO$ + "/" + Right$(Str$(scheduleNG%(targetGame%, 18)), 2)

    Call NewGame(V$, H$, YN$, DT$, TM$, NTMS, targetGame%)

End Sub

'----------------------------------------
'           NewGame SubRoutine
'----------------------------------------
'This subroutine gives the user an interface
'for editing and choosing options for the
'specified game in the schedule
Sub NewGame (visitor$, home$, YN$, DT$, TM$, NTMS, gameIndex%)

    Dim displayPause!

    Shared scheduleH$(), scheduleV$()
    Shared scheduleYN$()

    Shared scheduleNG%(), teamRatings()

    Shared coachMode$(), foulShotOpt$()
    Shared location$(), playMode$()
    Shared sClockOpt$(), yesNoText$()

    Do

        ' ** Enter New Game **
        Color 15, 0
        Cls

        Color 2
        Print Tab(29); "SELECT OPENING OPTIONS"

        Locate 2, 2: Color 14
        Print "(1) ";
        Color 15: Print playMode$(scheduleNG%(gameIndex%, 8))
        Locate 3, 2: Color 14
        Print "(2) ";
        Color 15: Print "VISITING TEAM: "; visitor$
        Locate 4, 2: Color 14
        Print "(3) ";
        Color 15: Print "HOME TEAM: "; home$
        Locate 5, 2: Color 14
        Print "(4) ";
        Color 15: Print "GAME DATE (XX-XX-XX)  "; DT$
        Locate 6, 2: Color 14
        Print "(5) ";
        Color 15: Print "GAME TIME (XX:XXP)    "; TM$
        Locate 7, 2: Color 14
        Print "(6) ";
        Color 15: Print Using "PLAY-BY-PLAY DELAY (SECS): ##.#  "; scheduleNG%(gameIndex%, 4) / 10
        Locate 8, 2: Color 14
        Print "(7) ";
        Color 15: Print "GAME LOCATION: "; location$(scheduleNG%(gameIndex%, 2))
        Locate 9, 2: Color 14
        Print "(8) ";
        Color 15: Print "SHOT CLOCK: "; sClockOpt$(scheduleNG%(gameIndex%, 7))
        Locate 10, 2: Color 14
        Print "(9) ";
        Color 15: Print "3-POINT SHOT: "; yesNoText$(scheduleNG%(gameIndex%, 3))
        Locate 11, 2: Color 14
        Print "(A) ";
        Color 15: Print "FOULS TO DISQUALIFY: "; scheduleNG%(gameIndex%, 5)
        Locate 12, 2: Color 14
        Print "(B) ";
        Color 15: Print "FREE THROWS ON 10TH FOUL: "; foulShotOpt$(scheduleNG%(gameIndex%, 6))
        Locate 13, 2: Color 14
        Print "(C) ";
        Color 15: Print "HUMAN COACHES: "; coachMode$(scheduleNG%(gameIndex%, 9))
        Locate 14, 2: Color 14
        Print "(D) ";
        Color 15: Print "3-FT WHEN FOULED ON 3-PT ATTEMPT: "; yesNoText$(scheduleNG%(gameIndex%, 10))
        Locate 15, 2: Color 14
        Print "(E) ";
        Color 15: Print "1 FT ON 1ST 6 FLOOR FOULS: "; yesNoText$(scheduleNG%(gameIndex%, 14))
        Locate 17, 2: Color 14
        Print "(0) ";
        Color 15: Print "ACCEPT SETTINGS"

        Do

            I$ = GetKeyPress$

            Call LetterToNumber(I$, setting)

            Select Case setting

                Case 1:
                    scheduleNG%(gameIndex%, 8) = scheduleNG%(gameIndex%, 8) + 1
                    If scheduleNG%(gameIndex%, 8) > 3 Then scheduleNG%(gameIndex%, 8) = 0

                Case 2, 3:

                    teamNbr = 0
                    Call ChooseTeam(YN$, NTMS, teamNbr, team$)

                    If teamNbr <> 0 Then

                        Color 15, 0
                        scheduleNG%(gameIndex%, setting - 2) = teamNbr
                        scheduleYN$(gameIndex%, setting - 2) = YN$

                        If setting = 2 Then
                            visitor$ = team$
                            scheduleV$(gameIndex%) = team$
                        End If

                        If setting = 3 Then
                            home$ = team$
                            scheduleH$(gameIndex%) = team$
                        End If

                    End If

                Case 4:
                    Locate 5, 26: Input DT$

                Case 5:
                    Locate 6, 26: Print "        "
                    Locate 6, 26: Input TM$

                Case 6:
                    Locate 7, 32: Input displayPause!
                    scheduleNG%(gameIndex%, 4) = displayPause! * 10

                Case 7:
                    N = 1 - (scheduleNG%(gameIndex%, 2) - 1)
                    scheduleNG%(gameIndex%, 2) = N + 1

                Case 8:
                    scheduleNG%(gameIndex%, 7) = 1 + scheduleNG%(gameIndex%, 7)
                    If scheduleNG%(gameIndex%, 7) > 3 Then scheduleNG%(gameIndex%, 7) = 0

                Case 9:
                    scheduleNG%(gameIndex%, 3) = 1 - scheduleNG%(gameIndex%, 3)

                Case 10:
                    scheduleNG%(gameIndex%, 5) = 1 + scheduleNG%(gameIndex%, 5)
                    If scheduleNG%(gameIndex%, 5) > 6 Then scheduleNG%(gameIndex%, 5) = 5

                Case 11:
                    scheduleNG%(gameIndex%, 6) = 1 - scheduleNG%(gameIndex%, 6)

                Case 12:
                    scheduleNG%(gameIndex%, 9) = 1 - scheduleNG%(gameIndex%, 9)

                Case 13:
                    scheduleNG%(gameIndex%, 10) = 1 - scheduleNG%(gameIndex%, 10) ' leave the loop / GoTo 149

                Case 14:
                    scheduleNG%(gameIndex%, 14) = 1 - scheduleNG%(gameIndex%, 14) ' leave the loop / GoTo 149

            End Select

        Loop Until (setting >= 0 And setting <= 14)

        If visitor$ <> "" And home$ <> "" And TM$ <> "" And DT$ <> "" Then

            meridian$ = Right$(TM$, 1)
            fullTime$ = Left$(TM$, Len(TM$) - 1)
            MIN% = Val(Right$(fullTime$, 2))
            hourString$ = Left$(fullTime$, Len(fullTime$) - 3)
            HR% = Val(hourString$)

            If UCase$(meridian$) = "P" And HR% < 12 Then
                'If user typed in PM, we are storing as military time
                'We don't check that military time may have been used
                HR% = HR% + 12
            End If

            'Gane Time
            scheduleNG%(gameIndex%, 15) = HR% * 100 + MIN%

            'Month
            scheduleNG%(gameIndex%, 17) = Val(Left$(DT$, 2))

            'Day
            scheduleNG%(gameIndex%, 16) = Val(Mid$((DT$), 4, 2))

            'Year
            scheduleNG%(gameIndex%, 18) = Val(Mid$((DT$), 7, 2))

        End If

    Loop Until setting = 0

End Sub

'----------------------------------------
'       ExtractSchedule SubRoutine
'----------------------------------------
'This is the core subroutine to leverage
'when mass changes are needing to be made
'to a schedule. It essentially reads in
'the existing schedule parameters, prompts
'the user for the appropriate changes, and
'then writes the changes back to the orig-
'inal schedule file.
Sub ExtractSchedule (scheduleAction%, scheduleFile$)
    'scheduleAction% = 3: EXTRACT ONE TEAM'S SCHEDULE
    'scheduleAction% = 4: EXTRACT CONFERENCE SCHEDULE
    'scheduleAction% = 5: CHANGE THE MODE OF PLAY FOR A TEAM
    'scheduleAction% = 6: MAKE GLOBAL CHANGES TO A SCHEDULE

    Dim displayPause!

    Shared homeScores(), visitorScores()

    Shared scheduleAP%()
    Shared scheduleNG%()
    Shared teamRatings()
    Shared scheduleYN$()

    Shared memberIdx()
    Shared memberName$(), memberYear$()

    Select Case scheduleAction%

        Case 6
            Call ChangeGlobalSettings
            Call SaveScheduleFile(scheduleFile$)
            Call NewSchedule(scheduleFile$)

        Case 3, 4, 5

            If scheduleAction% = 3 Or scheduleAction% = 4 Then
                subSchedFile$ = _InputBox$("Schedule File Name", "Enter name for schedule file")
            End If

            If scheduleAction% = 4 And subSchedFile$ <> "" Then
                'Pick conference to extract
                conferenceFile$ = _OpenFileDialog$("Choose League Definition file", diskPaths$(2), "*.TMI", "League Definition files [.TMI]")
                'ChooseFile$(diskPaths$(2) + "*.TMI", "CONFERENCE", ExitCode%)

                If conferenceFile$ <> "" Then

                    Call ReadConferenceIndex(conferenceFile$)
                    numberTeams = TEAMS_PER_CONFERENCE

                    For I = 1 To scheduleAP%(0)

                        scheduleYN$(I, 0) = RTrim$(scheduleYN$(I, 0))
                        scheduleYN$(I, 1) = RTrim$(scheduleYN$(I, 1))

                        For I1 = 1 To numberTeams

                            If memberName$(I1) <> "XXX" Then

                                If memberIdx(I1) = scheduleNG%(I, 0) And memberYear$(I1) = scheduleYN$(I, 0) Or memberIdx(I1) = scheduleNG%(I, 1) And memberYear$(I1) = scheduleYN$(I, 1) Then

                                    Print
                                    Print "FOUND A MATCH"

                                    Open diskPaths$(3) + subSchedFile$ + ".SCD" For Random As #2 Len = SCHEDULE_SIZE_BYTES

                                    fileLength& = LOF(2)
                                    numberGames = fileLength& / SCHEDULE_SIZE_BYTES

                                    For X = 0 To 18
                                        Field #2, X * 2 As X$, 2 As Q$(X + 1), SCHEDULE_SIZE_BYTES - 2 - 2 * X As X$
                                    Next

                                    Field #2, 38 As X$, 2 As Q$(22), 2 As Q$(23), 5 As Q$(20), 5 As Q$(21)

                                    For X = 0 To 18:
                                        LSet Q$(X + 1) = MKI$(scheduleNG%(I, X))
                                    Next

                                    For X = 0 To 1:
                                        LSet Q$(20 + X) = scheduleYN$(I, X)
                                    Next

                                    LSet Q$(22) = MKI$(visitorScores(I))
                                    LSet Q$(23) = MKI$(homeScores(I))

                                    Put #2, numberGames + 1

                                    Close #2

                                    Exit For

                                End If 'Done checking memberIdx

                            End If 'Done checking for "XXX"

                        Next I1

                    Next I

                End If 'Done checking count

            Else

                If (scheduleAction% = 3 And subSchedFile$ = "") Then
                    'If user cancelled from name entry
                    teamNbr = 0
                Else
                    teamNbr = 0
                    Call ChooseTeam(YN$, numberTeams, teamNbr, teamName$)
                End If

                If teamNbr <> 0 Then

                    Color 15, 0
                    Cls

                    'Change mode of play
                    If scheduleAction% = 5 Then

                        Color teamRatings(0), teamRatings(1): Print teamName$;
                        Color 15, 0
                        Print " IS TO BE:"
                        Print
                        Color 12
                        Print "(0)";
                        Color 15
                        Print " MANUALLY CONTROLLED"
                        Print
                        Color 12
                        Print "(1)";
                        Color 15
                        Print " COMPUTER CONTROLLED"

                        Do
                            C$ = GetKeyPress$
                            I1 = Val(I$)
                        Loop Until C$ = "0" Or C$ = "1"

                        Print
                        Input "ENTER PLAY-BY-PLAY DELAY FOR THESE GAMES "; displayPause!

                    End If

                    For I = 1 To scheduleAP%(0)

                        scheduleYN$(I, 0) = RTrim$(scheduleYN$(I, 0))
                        scheduleYN$(I, 1) = RTrim$(scheduleYN$(I, 1))

                        If teamNbr = scheduleNG%(I, 0) And YN$ = scheduleYN$(I, 0) Or teamNbr = scheduleNG%(I, 1) And YN$ = scheduleYN$(I, 1) Then

                            'CHANGE SETTINGS FOR A TEAM
                            If teamNbr = scheduleNG%(I, 0) And YN$ = scheduleYN$(I, 0) Then
                                scheduleNG%(I, 8) = 2
                            End If

                            If teamNbr = scheduleNG%(I, 1) And YN$ = scheduleYN$(I, 1) Then
                                scheduleNG%(I, 8) = 1
                            End If

                            If (teamNbr = scheduleNG%(I, 0) And YN$ = scheduleYN$(I, 0) Or teamNbr = scheduleNG%(I, 1) And YN$ = scheduleYN$(I, 1)) And I1 = 1 And scheduleAction% = 4 Then
                                scheduleNG%(I, 8) = 3
                            End If

                            scheduleNG%(I, 4) = displayPause! * 10

                            'Extract only
                            If Not (scheduleAction% = 5) Then

                                Open diskPaths$(3) + subSchedFile$ + ".SCD" For Random As #3 Len = SCHEDULE_SIZE_BYTES
                                fileLength& = LOF(3)
                                totalGames = fileLength& / SCHEDULE_SIZE_BYTES

                                'Write matching games
                                Locate 1, 1: Print totalGames + 1; " MATCHES"

                                For X = 0 To 18
                                    Field #3, X * 2 As X$, 2 As Q$(X + 1), SCHEDULE_SIZE_BYTES - 2 - 2 * X As X$
                                Next

                                Field #3, 38 As X$, 2 As Q$(22), 2 As Q$(23), 5 As Q$(20), 5 As Q$(21)

                                For X = 0 To 18:
                                    LSet Q$(X + 1) = MKI$(scheduleNG%(I, X))
                                Next

                                For X = 0 To 1:
                                    LSet Q$(20 + X) = scheduleYN$(I, X)
                                Next

                                LSet Q$(22) = MKI$(visitorScores(I))
                                LSet Q$(23) = MKI$(homeScores(I))

                                Put #3, totalGames + 1

                                Close #3

                            End If 'Done checking schedule action

                        End If 'Done checking for matches

                    Next 'Done looping through games

                End If

            End If

    End Select

End Sub


'----------------------------------------
'       EditParameters SubRoutine
'----------------------------------------
'This subroutine gives the user an inter-
'face for editing the parameters by which
'"Special" boxscores are created / saved.
Sub EditParameters ()

    Shared eventSettings()

    'Read in existing parameters if there are any
    If _FileExists("EVENTS.BOX") Then

        Open "EVENTS.BOX" For Input As #1

        For X = 1 To 13
            Input #1, eventSettings(X)
        Next

        Close #1

    End If

    Cls
    Color 14
    Print "PARAMETERS FOR SAVING SPECIAL EVENTS BOXSCORES"

    Color 15

    Print " 1. OVERTIME PERIODS ARE OVER "; eventSettings(1)
    Print " 2. TOTAL POINTS GREATER THAN "; eventSettings(2)
    Print " 3. TOTAL REBOUNDS GREATER THAN "; eventSettings(3)
    Print " 4. TEAM POINTS GREATER THAN "; eventSettings(4)
    Print " 5. TEAM REBOUNDS GREATER THAN "; eventSettings(5)
    Print " 6. PLAYER'S POINTS MORE THAN "; eventSettings(6)
    Print " 7. PLAYER'S REBOUNDS MORE THAN "; eventSettings(7)
    Print " 8. PLAYER'S ASSISTS MORE THAN "; eventSettings(8)
    Print " 9. PLAYER'S BLOCKS MORE THAN "; eventSettings(9)
    Print "10. PLAYER'S STEALS MORE THAN "; eventSettings(10)
    Print "11. PLAYER'S 3-FGM MORE THAN "; eventSettings(11)

    C1 = 1

    Do

        Locate LAST_ROW - 2, 1: Print "SELECT # TO CHANGE 1-11 (0 TO CONTINUE) ";
        Input B9

        Select Case B9

            Case 1:
                Locate 2, 30: Print Space$(10)
                Locate 2, 1: Input " 1. OVERTIME PERIODS ARE OVER"; eventSettings(1)
                Locate 2, 30: Print " "

            Case 2:
                Locate 3, 30: Print Space$(10)
                Locate 3, 1: Input " 2. TOTAL POINTS GREATER THAN"; eventSettings(2)
                Locate 3, 30: Print " "

            Case 3:
                Locate 4, 32: Print Space$(10)
                Locate 4, 1: Input " 3. TOTAL REBOUNDS GREATER THAN"; eventSettings(3)
                Locate 4, 32: Print " "

            Case 4:
                Locate 5, 29: Print Space$(10)
                Locate 5, 1: Input " 4. TEAM POINTS GREATER THAN"; eventSettings(4)
                Locate 5, 29: Print " "

            Case 5:
                Locate 6, 31: Print Space$(10)
                Locate 6, 1: Input " 5. TEAM REBOUNDS GREATER THAN"; eventSettings(5)
                Locate 6, 31: Print " "

            Case 6:
                Locate 7, 30: Print Space$(10)
                Locate 7, 1: Input " 6. PLAYER'S POINTS MORE THAN"; eventSettings(6)
                Locate 7, 30: Print " "

            Case 7:
                Locate 8, 32: Print Space$(10)
                Locate 8, 1: Input " 7. PLAYER'S REBOUNDS MORE THAN"; eventSettings(7)
                Locate 8, 33: Print " "

            Case 8:
                Locate 9, 31: Print Space$(10)
                Locate 9, 1: Input " 8. PLAYER'S ASSISTS MORE THAN"; eventSettings(8)
                Locate 9, 31: Print " "

            Case 9:
                Locate 10, 30: Print Space$(10)
                Locate 10, 1: Input " 9. PLAYER'S BLOCKS MORE THAN"; eventSettings(9)
                Locate 10, 30: Print " "

            Case 10:
                Locate 11, 30: Print Space$(10)
                Locate 11, 1: Input "10. PLAYER'S STEALS MORE THAN"; eventSettings(10)
                Locate 11, 30: Print " "

            Case 11:
                Locate 12, 29: Print Space$(10)
                Locate 12, 1: Input "11. PLAYER'S 3-FGM MORE THAN"; eventSettings(11)
                Locate 12, 29: Print " "

        End Select

    Loop Until B9 = 0 'ENTER also counts as 0

    'Save all changes
    Open "EVENTS.BOX" For Output As #2

    For X = 1 To 13
        Print #2, eventSettings(X)
    Next

    Close #2

End Sub


'----------------------------------------
'    BoxscoreMaintenance SubRoutine
'----------------------------------------
'This subroutine gives the user a menu
'for several of the options available to
'manage and maintain boxscores.
Sub BoxscoreMaintenance ()

    ReDim menuChoices$(1 To 6)

    menuChoices$(1) = "SET PARAMETERS TO SAVE SPECIAL EVENTS BOXSCORES  "
    menuChoices$(2) = "ACCESS USER SAVED AND SPECIAL EVENTS BOXSCORES   "
    menuChoices$(3) = "DELETE ALL SCHEDULE GENERATED BOXSCORES          "
    menuChoices$(4) = "DELETE ALL USER SAVED, & SPECIAL BOXSCORES       "
    menuChoices$(5) = "DELETE ALL BOXSCORES (SCHEDULE, USER, & SPECIAL)"
    menuChoices$(6) = "RETURN TO MAIN AUTO SCHEDULER MENU               "
    SELECTION% = 1
    Count% = 6
    Start% = 1
    ExitCode% = 6
    FileFGColor = 15: FileBGColor = 1
    HiLiteFGColor% = 0: HiLiteBGColor% = 7
    Spacing% = 1
    Row% = 8
    menuWidth% = Len(menuChoices$(1))
    Column% = (80 - menuWidth% - 2) / 2

    Do

        Color 15, 0

        Call TITLE_SCHEDULER

        Call box0(Row% - 1, Column% - 1, Row% + Count%, Column% + menuWidth%, 2, FileFGColor, FileBGColor)

        _MouseShow "DEFAULT"
        MMenuVert menuChoices$(), SELECTION%, Start%, ExitCode%, FileFGColor, FileBGColor, HiLiteFGColor%, HiLiteBGColor%, Count%, Row%, Column%
        _MouseHide

        If ExitCode% <> 27 Then

            Select Case SELECTION%

                Case 1:
                    Call EditParameters

                Case 2:
                    boxscoreFile$ = ChooseFile$(diskPaths$(1) + "*.BSS", "BOXSCORE", ExitCode%)
                    'boxscoreFile$ = ChooseFile$(diskPaths$(1) + "*.SPC", "BOXSCORE", ExitCode%)

                    If ExitCode% <> 27 And ExitCode% <> 0 And boxscoreFile$ <> "" Then
                        boxName$ = diskPaths$(1) + boxscoreFile$
                        Call EditBoxscore(boxName$ + ".BSS")
                        'Call EditBoxscore(boxName$ + ".SPC")
                    End If

                Case 3
                    Spec$ = diskPaths$(1) + "*.BS"
                    'Spec$ = diskPaths$(1) + "BOX*."
                    Call DeleteBoxscores(SELECTION%, Spec$)

                Case 4
                    Spec$ = diskPaths$(1) + "*.BSS"
                    'Spec$ = diskPaths$(1) + "*.SPC"
                    Call DeleteBoxscores(SELECTION%, Spec$)

                Case 5:
                    Spec$ = diskPaths$(1) + "*.BS"
                    'Spec$ = diskPaths$(1) + "BOX*."
                    Call DeleteBoxscores(SELECTION%, Spec$)

                    Spec$ = diskPaths$(1) + "*.BSS"
                    'Spec$ = diskPaths$(1) + "*.SPC"
                    Call DeleteBoxscores(SELECTION%, Spec$)

            End Select

        End If 'Done checking scancode

    Loop Until SELECTION% = 6 Or ExitCode% = 27

End Sub


'----------------------------------------
'       SaveScheduleFile SubRoutine
'----------------------------------------
'This subroutine writes all scheduled data,
'as stored in the relevant arrays, back to
'the target schedule file.
Sub SaveScheduleFile (saveFile$)

    Shared homeScores(), visitorScores()
    Shared scheduleAP%(), scheduleNG%()
    Shared scheduleYN$()

    If _FileExists(saveFile$) Then Kill saveFile$

    Open saveFile$ For Random As #1 Len = SCHEDULE_SIZE_BYTES

    For X = 0 To 18
        Field #1, X * 2 As X$, 2 As Q$(X + 1), SCHEDULE_SIZE_BYTES - 2 - 2 * X As X$
    Next

    Field #1, 38 As X$, 2 As Q$(22), 2 As Q$(23), 5 As Q$(20), 5 As Q$(21)

    For I = 1 To scheduleAP%(0)

        For X = 0 To 18:
            LSet Q$(X + 1) = MKI$(scheduleNG%(I, X))
        Next

        For X = 0 To 1:
            LSet Q$(20 + X) = RTrim$(scheduleYN$(I, X))
        Next

        LSet Q$(22) = MKI$(visitorScores(I))
        LSet Q$(23) = MKI$(homeScores(I))

        Put #1, I

    Next

    Close #1

End Sub


'----------------------------------------
'       SortSchedule SubRoutine
'----------------------------------------
'This subroutine is used to automatically
'sort all games in the schedule by their
'date / time.
Sub SortSchedule ()

    Dim schedDates&(MAX_SCHEDULE_GAMES)
    Dim D&, M&, Y&

    Shared homeScores(), visitorScores()
    Shared scheduleAP%(), scheduleNG%()
    Shared scheduleYN$()

    For I = 1 To scheduleAP%(0)
        HR% = Int(scheduleNG%(I, 15) / 100)
        MIN% = scheduleNG%(I, 15) - HR% * 100
        TM% = HR% * 60 + MIN%
        scheduleNG%(I, 12) = Int(TM% / 30 - 18)
        D& = (scheduleNG%(I, 18) - 1)
        D& = D& * 365
        M& = (scheduleNG%(I, 17) - 1)
        M& = M& * 31
        Y& = scheduleNG%(I, 16)
        schedDates&(I) = (D& + M& + Y&) * 24 * 60 + TM%
    Next

    For Z = 1 To scheduleAP%(0) - 1

        For Z1 = Z + 1 To scheduleAP%(0)

            Locate 1, 1: Print Z, Z1

            If scheduleNG%(Z, 12) > scheduleNG%(Z1, 12) Then

                For I = 0 To 18:
                    Swap scheduleNG%(Z, I), scheduleNG%(Z1, I)
                Next

                For I = 0 To 1:
                    Swap scheduleYN$(Z, I), scheduleYN$(Z1, I)
                Next

                Swap schedDates&(Z), schedDates&(Z1)
                Swap visitorScores(Z), visitorScores(Z1)
                Swap homeScores(Z), homeScores(Z1)

            End If

        Next Z1

    Next Z

    For Z = 1 To scheduleAP%(0) - 1

        For Z1 = Z + 1 To scheduleAP%(0)

            If schedDates&(Z) > schedDates&(Z1) Then

                For I = 0 To 18:
                    Swap scheduleNG%(Z, I), scheduleNG%(Z1, I)
                Next

                For I = 0 To 1:
                    Swap scheduleYN$(Z, I), scheduleYN$(Z1, I)
                Next

                Swap schedDates&(Z), schedDates&(Z1)
                Swap visitorScores(Z), visitorScores(Z1)
                Swap homeScores(Z), homeScores(Z1)

            End If

        Next Z1

    Next Z

End Sub


'----------------------------------------
'    ChangeGlobalSettings SubRoutine
'----------------------------------------
'This subroutine presents the end user a
'variety of game options that can be changed
'or applied to all games in the schedule file.
'These would be game-specific options, as
'opposed to teams / dates / times.
Sub ChangeGlobalSettings ()

    Shared scheduleAP%(), scheduleNG%()

    Shared coachMode$(), foulShotOpt$()
    Shared location$(), playMode$()
    Shared sClockOpt$(), yesNoText$()

    Dim displayPause!

    ' GLOBAL SETTINGS FOR A SCHEDULE
    ' The code below would use default values based on the most recent rules
    'playerOpt = 3
    'sClockVal = 3
    'coachOpt = 1
    'threePtOpt = 1
    'foulsToDQ = 5
    'ft10thFoul = 0
    'ft3PtFoul = 1
    'ft6FloorFouls = 0

    'This code will assign defaults based on the first game in the schedule.
    'Usually when I want to change global settings, I want to keep most
    'settings in the schedule the same.
    playerOpt = scheduleNG%(1, 8)
    sClockVal = scheduleNG%(1, 7)
    threePtOpt = scheduleNG%(1, 3)
    foulsToDQ = scheduleNG%(1, 5)
    ft10thFoul = scheduleNG%(1, 6)
    displayPause! = scheduleNG%(1, 4) / 10
    coachOpt = scheduleNG%(1, 9)
    ft3PtFoul = scheduleNG%(1, 10)
    ft6FloorFouls = scheduleNG%(1, 14)

    DT$ = "11-04-24"
    TM$ = "07:00P"

    Do

        Color 15, 0
        Cls

        Print Tab(30); "GLOBAL SETTINGS FOR SCHEDULE"

        Locate 3, 2: Color 14: Print "(1) ";
        Color 15: Print "GAME PLAY: "; playMode$(playerOpt)
        Locate 4, 2: Color 14: Print "(2) ";
        Color 15: Print "SHOT CLOCK: "; sClockOpt$(sClockVal)
        Locate 5, 2: Color 14: Print "(3) ";
        Color 15: Print "3-POINT SHOT: "; yesNoText$(threePtOpt)
        Locate 6, 2: Color 14: Print "(4) ";
        Color 15: Print Using "FOULS TO DISQUALIFY: ##"; foulsToDQ
        Locate 7, 2: Color 14: Print "(5) ";
        Color 15: Print "FREE THROWS ON 10TH FOUL: "; foulShotOpt$(ft10thFoul)
        Locate 8, 2: Color 14: Print "(6) ";
        Color 15: Print Using "PLAY-BY-PLAY DELAY (SECS): ##.#  "; displayPause!
        Locate 9, 2: Color 14: Print "(7) ";
        Color 15: Print "HUMAN COACHES: "; coachMode$(coachOpt)
        Locate 10, 2: Color 14: Print "(8) ";
        Color 15: Print "3-FT WHEN FOULED ON 3-PT ATTEMPT: "; yesNoText$(ft3PtFoul)
        Locate 11, 2: Color 14: Print "(9) ";
        Color 15: Print "1 FT ON 1ST 6 FLOOR FOULS: "; yesNoText$(ft6FloorFouls)

        Locate 13, 2: Color 14: Print "(0) ";
        Color 15: Print "ACCEPT GLOBAL SETTINGS"

        Do

            I$ = GetKeyPress$

            Call LetterToNumber(I$, setting)

        Loop Until setting >= 0 And setting <= 10

        Select Case setting

            Case 1:
                playerOpt = playerOpt + 1
                If playerOpt > 3 Then playerOpt = 0

            Case 2:
                sClockVal = sClockVal + 1
                'If sClockVal > 2 Then sClockVal = 0
                If sClockVal > 3 Then sClockVal = 0

            Case 3:
                threePtOpt = 1 - threePtOpt

            Case 4:
                foulsToDQ = foulsToDQ + 1
                If foulsToDQ > 6 Then foulsToDQ = 5

            Case 5:
                ft10thFoul = 1 - ft10thFoul

            Case 6:
                Locate 8, 32: Input displayPause!

            Case 7:
                coachOpt = 1 - coachOpt

            Case 8:
                ft3PtFoul = 1 - ft3PtFoul

            Case 9:
                ft6FloorFouls = 1 - ft6FloorFouls

        End Select

    Loop Until setting = 0

    For I = 1 To scheduleAP%(0)
        scheduleNG%(I, 3) = threePtOpt
        scheduleNG%(I, 4) = displayPause! * 10
        scheduleNG%(I, 5) = foulsToDQ
        scheduleNG%(I, 6) = ft10thFoul
        scheduleNG%(I, 7) = sClockVal
        scheduleNG%(I, 8) = playerOpt
        scheduleNG%(I, 9) = coachOpt
        scheduleNG%(I, 10) = ft3PtFoul
        scheduleNG%(I, 14) = ft6FloorFouls
    Next

End Sub


'----------------------------------------
'       DeleteBoxscores SubRoutine
'----------------------------------------
'This subroutine prompts for confirmation
'of and proceeds to delete the specified
'types of boxscores. This is called by
'BoxScore maintenance routine.
Sub DeleteBoxscores (fileChoice%, fileSpec$)

    Spec$ = diskPaths$(0) + fileSpec$
    Count% = FileCount%(Spec$)

    If Count% = 0 Then

        Select Case fileChoice%

            Case 3
                MsgText$ = "NO BOXSCORES WERE FOUND PRESS ANY KEY TO RETURN TO MENU"

            Case 4
                MsgText$ = "NO SPECIAL EVENTS BOXSCORES WERE FOUND PRESS ANY KEY TO RETURN TO MENU"

            Case 5
                ' Exit Sub

        End Select

        BoxWidth = 24
        BoxHeight = (Len(MsgText$) / BoxWidth) + 3 'top and box lines + 1 extra
        Locate ((LAST_ROW - BoxHeight) / 2), (80 - ((BoxWidth + 2) / 2))
        Call CSMsgBox(MsgText$, BoxWidth, PASS)
        I$ = GetKeyPress$
        Call CSMsgBox("", 0, PASS)

    Else

        ReDim boxFiles$(0 To Count%)

        For X = 1 To Count%
            boxFiles$(X) = Space$(12)
        Next

        boxFiles$(0) = fileSpec$
        Call ReadFile(boxFiles$())

        For X = 1 To Count%
            Kill (diskPaths$(1) + boxFiles$(X))
            Locate 12, 34: Print "DELETING "; boxFiles$(X)
        Next

        MsgText$ = "ALL BOXSCORE FILES HAVE BEEN DELETED. PRESS ANY KEY TO RETURN TO MAIN MENU"
        BoxWidth = 20
        BoxHeight = (Len(MsgText$) / BoxWidth) + 3 'top and box lines + 1 extra
        Locate ((LAST_ROW - BoxHeight) / 2), (80 - ((BoxWidth + 2) / 2))
        Call CSMsgBox(MsgText$, BoxWidth, PASS)
        I$ = GetKeyPress$
        Call CSMsgBox("", 0, PASS)

    End If

End Sub


'----------------------------------------
'      LoadScheduleInfo SubRoutine
'----------------------------------------
'This subroutine opens the target SCHEDULE
'file and reads the data into corresponding
'arrays for use in the scheduler
Sub LoadScheduleInfo (targetFile$)

    Shared BS%, NS%

    Shared scheduleAP%(), scheduleNG%()
    Shared homeScores(), visitorScores()
    Shared scheduleYN$()

    ReDim scheduleNG%(MAX_SCHEDULE_GAMES, 18)
    ReDim scheduleYN$(MAX_SCHEDULE_GAMES, 1)
    ReDim homeScores(MAX_SCHEDULE_GAMES)
    ReDim visitorScores(MAX_SCHEDULE_GAMES)

    Open targetFile$ For Random As #1 Len = SCHEDULE_SIZE_BYTES

    fileLength& = LOF(1)
    scheduleAP%(0) = fileLength& / SCHEDULE_SIZE_BYTES
    BS% = Int((scheduleAP%(0) - 1) / SCHED_ENTRIES_PER_SCREEN)

    For X = 0 To 18
        Field #1, X * 2 As X$, 2 As Q$(X + 1), SCHEDULE_SIZE_BYTES - 2 - 2 * X As X$
    Next

    Field #1, 38 As X$, 2 As Q$(22), 2 As Q$(23), 5 As Q$(20), 5 As Q$(21)

    For I = 1 To scheduleAP%(0)

        Get #1, I

        For X = 0 To 18
            scheduleNG%(I, X) = CVI(Q$(X + 1))
        Next

        For X = 0 To 1
            scheduleYN$(I, X) = RTrim$(Q$(20 + X))
        Next

        visitorScores(I) = CVI(Q$(22))
        homeScores(I) = CVI(Q$(23))

    Next

    Close #1

    NS% = 0

End Sub


'----------------------------------------
'   CreateConferenceSchedule SubRoutine
'----------------------------------------
'This subroutine gives the user an inter-
'face by which they can create a standard
'season schedule for a Basketball conference.
Sub CreateConferenceSchedule ()

    Shared BS%

    Shared scheduleAP%(), homeScores(), visitorScores()
    Shared scheduleNG%(), memberIdx()
    Shared scheduleYN$()
    Shared memberName$(), memberYear$()

    Cls

    schedSizeOpt = 0

    Locate 5, 1: Color , 0
    Color 15: Print "(1) ";
    Color 14: Print "6 TEAM, 30 GAME SCHEDULE"
    Color 15: Print "(2) ";
    Color 14: Print "7 TEAM, 42 GAME SCHEDULE"
    Color 15: Print "(3) ";
    Color 14: Print "8 TEAM, 56 GAME SCHEDULE"
    Color 15: Print "(4) ";
    Color 14: Print "9 TEAM, 72 GAME SCHEDULE"
    Color 15: Print "(5) ";
    Color 14: Print "10 TEAM, 90 GAME SCHEDULE"
    Color 15: Print "(6) ";
    Color 14: Print "11 TEAM, 88 GAME SCHEDULE"
    Color 15: Print "(7) ";
    Color 14: Print "12 TEAM  96 GAME SCHEDULE"
    Color 15: Print "(8) ";
    Color 14: Print "13 TEAM 117 GAME SCHEDULE"
    Color 15: Print "(9) ";
    Color 14: Print "16 TEAM 128 GAME SCHEDULE"
    Color 15: Print "(0) ";
    Color 14: Print "RETURN TO COURTSIDE MAIN MENU"

    Do
        I$ = GetKeyPress$
        schedSizeOpt = Val(I$)
    Loop Until (schedSizeOpt > 0 And schedSizeOpt <= 9) Or (I$ = Chr$(27))

    If I$ <> Chr$(27) Then

        conferenceFile$ = _OpenFileDialog$("Choose League Definition file", diskPaths$(2), "*.TMI", "League Definition files [.TMI]")
        'ChooseFile$(diskPaths$(2) + "*.TMI", "CONFERENCE", ExitCode%)

        If conferenceFile$ <> "" Then

            Open conferenceFile$ For Input As #1

            X1 = 0

            Input #1, statType
            Input #1, leagueName$
            Input #1, div1$
            Input #1, div2$

            For X = 1 To TEAMS_PER_CONFERENCE

                Input #1, confTeam$
                Input #1, teamIdx
                Input #1, teamDiskID$

                If confTeam$ <> "XXX" And confTeam$ <> "" And confTeam$ <> "*" Then
                    X1 = X1 + 1
                    memberName$(X1) = confTeam$
                    memberIdx(X1) = teamIdx
                    memberYear$(X1) = teamDiskID$
                End If

            Next

            Close #1

            Select Case schedSizeOpt
                Case 1: templateFile$ = "6TM.SCX"
                Case 2: templateFile$ = "7TM.SCX"
                Case 3: templateFile$ = "8TM.SCX"
                Case 4: templateFile$ = "9TM.SCX"
                Case 5: templateFile$ = "10TM.SCX"
                Case 6: templateFile$ = "11TM.SCX"
                Case 7: templateFile$ = "12TM.SCX"
                Case 8: templateFile$ = "13TM.SCX"
                Case 9: templateFile$ = "16TM.SCX"
            End Select

            'Read in SCX file as a "template" for our SCD
            'The file literally has 0 values for everything
            Open templateFile$ For Random As #2 Len = SCHEDULE_SIZE_BYTES

            fileLength& = LOF(2)

            scheduleAP%(0) = fileLength& / SCHEDULE_SIZE_BYTES
            BS% = Int((scheduleAP%(0) - 1) / SCHED_ENTRIES_PER_SCREEN)

            For X = 0 To 18
                Field #2, X * 2 As X$, 2 As Q$(X + 1), SCHEDULE_SIZE_BYTES - 2 - 2 * X As X$
            Next

            Field #2, 38 As X$, 2 As Q$(22), 2 As Q$(23), 5 As Q$(20), 5 As Q$(21)

            For currGame = 1 To scheduleAP%(0)

                Get #2, currGame

                For X = 0 To 18:
                    scheduleNG%(currGame, X) = CVI(Q$(X + 1))
                Next
                For X = 0 To 1:
                    scheduleYN$(currGame, X) = Q$(20 + X)
                Next

                visitorScores(currGame) = CVI(Q$(22))
                homeScores(currGame) = CVI(Q$(23))

            Next

            Close #2

            'Use "Change Global" option to assing defaults
            Call ChangeGlobalSettings

            Color 15, 0
            Cls

            scheduleFile$ = _InputBox$("Schedule File Name", "Enter name for schedule file")

            If scheduleFile$ <> "" Then

                For I = 1 To scheduleAP%(0)

                    Print "GAME"; I
                    Print scheduleNG%(I, 0), scheduleYN$(I, 0)
                    Print scheduleNG%(I, 1), scheduleYN$(I, 1)

                    'Replace template values with those from global change
                    X = scheduleNG%(I, 0)
                    scheduleNG%(I, 0) = memberIdx(X)
                    scheduleYN$(I, 0) = memberYear$(X)

                    X = scheduleNG%(I, 1)
                    scheduleNG%(I, 1) = memberIdx(X)
                    scheduleYN$(I, 1) = memberYear$(X)

                    Print "GAME"; I
                    Print scheduleNG%(I, 0), scheduleYN$(I, 0)
                    Print scheduleNG%(I, 1), scheduleYN$(I, 1)

                Next

                'Save modified file (essentially a copy of the template)
                Open diskPaths$(3) + scheduleFile$ + ".SCD" For Random As #3 Len = SCHEDULE_SIZE_BYTES

                nbrSchedGames = LOF(3) / SCHEDULE_SIZE_BYTES

                For X = 0 To 18
                    Field #3, X * 2 As X$, 2 As Q$(X + 1), SCHEDULE_SIZE_BYTES - 2 - 2 * X As X$
                Next

                Field #3, 38 As X$, 2 As Q$(22), 2 As Q$(23), 5 As Q$(20), 5 As Q$(21)

                For I = 1 To scheduleAP%(0)

                    For X = 0 To 18:
                        LSet Q$(X + 1) = MKI$(scheduleNG%(I, X))
                    Next

                    For X = 0 To 1:
                        LSet Q$(20 + X) = scheduleYN$(I, X)
                    Next

                    LSet Q$(22) = MKI$(visitorScores(I))
                    LSet Q$(23) = MKI$(homeScores(I))

                    'This should be appending items to the end
                    Put #3, nbrSchedGames + I

                Next

                Close #3

            End If 'Done checking for schedule file name

        End If 'Done checking for ESC

    End If 'Done checking for ESC

End Sub


'----------------------------------------
'       EditBoxscore SubRoutine
'----------------------------------------
'This subroutine is designed to facilitate
'the user's ability to edit, in-realtime,
'a boxscore that has been previously created
'by the game. It is essentially a DOS-based
'text editing routine, which was written in
'x86 asm. Therefore, the corresponding code
'is no longer available or useful. It is
'recommended that users simply edit their
'boxscores in their favorite text editor.
Sub EditBoxscore (boxName$)

    If Not _FileExists(boxName$) Then

        MsgText$ = "BOXSCORE DOES NOT EXIST PRESS ANY KEY TO RETURN TO MAIN MENU"
        BoxWidth = 24
        BoxHeight = (Len(MsgText$) / BoxWidth) + 3 'top and box lines + 1 extra
        Locate ((LAST_ROW - BoxHeight) / 2), (80 - ((BoxWidth + 2) / 2))
        Call CSMsgBox(MsgText$, BoxWidth, PASS)
        I$ = GetKeyPress$
        Call CSMsgBox("", 0, PASS)

    Else

        Call QBWRITE(boxName$)

    End If

End Sub
